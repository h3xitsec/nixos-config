# Mutability module: copy mutable files after linkGeneration so parent dirs exist.
# Enables home.file.*.mutable and runs mutableFileGeneration after linkGeneration.
# From: https://gist.github.com/h3xitsec/b5922a7c727e0bfd74c56a35b379f837
{
  config,
  lib,
  ...
}: let
  fileOptionAttrPaths = [["home" "file"] ["xdg" "configFile"] ["xdg" "dataFile"]];
in {
  options = let
    mergeAttrsList = builtins.foldl' (lib.mergeAttrs) {};

    fileAttrsType = lib.types.attrsOf (lib.types.submodule ({config, ...}: {
      options.mutable = lib.mkOption {
        type = lib.types.bool;
        default = false;
        description = ''
          Whether to copy the file without the read-only attribute instead of
          symlinking. If you set this to `true`, you must also set `force` to
          `true`. Mutable files are not removed when you remove them from your
          configuration.
          This option is useful for programs that don't have a very good
          support for read-only configurations.
        '';
      };
    }));
  in
    mergeAttrsList (map (attrPath:
      lib.setAttrByPath attrPath (lib.mkOption {type = fileAttrsType;}))
    fileOptionAttrPaths);

  config = {
    home.activation.mutableFileGeneration = let
      allFiles = builtins.concatLists (map
        (attrPath: builtins.attrValues (lib.getAttrFromPath attrPath config))
        fileOptionAttrPaths);

      filterMutableFiles = builtins.filter (file:
        (file.mutable or false)
        && lib.assertMsg file.force
        "if you specify `mutable` to `true` on a file, you must also set `force` to `true`");

      mutableFiles = filterMutableFiles allFiles;

      toCommand = file: let
        source = lib.escapeShellArg (toString file.source);
        # Use $HOME so the copy works regardless of activation script cwd
        targetPart = lib.escapeShellArg file.target;
      in ''
        target="$HOME"/${targetPart}
        $VERBOSE_ECHO "${source} -> $target"
        $DRY_RUN_CMD mkdir -p $VERBOSE_ARG "$(dirname "$target")"
        $DRY_RUN_CMD cp --remove-destination --no-preserve=mode ${source} "$target"
      '';

      command =
        ''
          echo "Copying mutable home files for $HOME"
        ''
        + lib.concatStringsSep "\n" (map toCommand mutableFiles);
    in (lib.hm.dag.entryAfter ["linkGeneration"] command);
  };
}
